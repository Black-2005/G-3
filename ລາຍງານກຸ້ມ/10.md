# **Reflex Fight****ທ ເພັດນະຄອນ ຄໍາພັນ ເບີ 34 **3ຄອມ1**

ໃນໂປຮເຈັກນີ້ເຮົາຈະພາມາເຮັດເກມໂດຍໃຊ້ອຸປະກອນໃນ Tinkercad ໂດຍໃນໂປຮເຈັກນີ້ຈະຊ່ວຍໃຫ້ຜູ້ຫຼິ້ນຝຶກຝົນຄວາມໄວ ແລະ ການຕອບສະຫນອງໄວຍິ່ງຂຶ້ນ. ເກມ “Reflex Fight” ແມ່ນເກມທີ່ອອກແບບມາເພື່ອທົດສອບປະກຏິລິຍາຂອງຜູ້ຫຼິ້ນ ໂດຍໃຊ້ເຄື່ອງມືຕ່າງໆໃນ Tinkercad ເຊັ່ນ 8x8 Dot Matrix, Sensor PIR ຫຼື Ultrasonic, LED, Buzzer ແລະ ອື່ນໆ.

ໃນເກມ “Reflex Fight” ນີ້ ຈະເປັນການແຂ່ງຂັນກັນລະຫວ່າງຜູ້ຫຼິ້ນ 2 ຄົນ ໂດຍຈຸດປະສົງແມ່ນເພື່ອທົດສອບຄວາມໄວໃນການຕອບສະນອງ. ເມື່ອກົດປຸ້ມລະບົບສົ່ງສຽງແຈ້ງເຕືອນອອກມາ ຜູ້ຫຼິ້ນຈະຕ້ອງເຄື່ອນໄຫວຜ່ານ Sensor PIR ຫຼື Ultrasonic ໃຫ້ໄວ ແລະ ຖ້າໃຜຕອບສະຫນອງກ່ອນ ຄົນນັ້ນຈະໄດ້ຮັບຄະແນນ ແລະ ຈະຊະນະໃນຕານັ້ນ. ລະບົບຈະສົ່ງສຽງດັງຂຶ້ນ ແລະ ເປີດໄຟ LED ໃຫ້ກັບຜູ້ຫຼິ້ນທີ່ຕອບທີ່ໄວກວ່າເພື່ອໃຫ້ຮູ້ວ່າຜູ້ຫລີ້ນຄົນນັ້ນເປັນຝ່າຍຊະນະໄປຖ້າເຮົາຢາກເລີ່ມຫລີ້ນໃຫມ່ແມ່ນໃຫ້ກົດປຸ້ມຄືເກົ່າ.

ມັນເປັນເກມທີ່ສ້າງຄວາມຕື່ນເຕັ້ນ ແລະ ພັດທະນາທັກສະປະຏກິລິຍາໄດ້ເປັນຢ່າງດີ ໂດຍພາຍໃນແບບຈຳລອງດ້ວຍ Tinkercad ທີ່ເຮົາສ້າງຂຶ້ນເອງ.
#### ອຸປະກອນ
•  Arduino UNO ×1
•  LCD 16x2 Display ×1
•  Ultrasonic Sensor ×2
•  Buzzer ×1
•  LED (P1) ×1
•  LED (P2) ×1
•  Push Button ×1
•   220Ω (LED) ×2
•   10kΩ (Push Button) ×1
•  Breadboard + Jumper Wires
#### ການເຊື່ອມວົງຈອນ
LCD RS    ຂາ 12     
LCD E      ຂາ 11
LCD D4    ຂາ 5 
LCD D5    ຂາ 4 
LCD D6    ຂາ 3 
LCD D7    ຂາ2  
PIR 1 (OUT)      ຂາ 7 Player 1 Sensor
PIR 2 (OUT)      ຂາ 8 Player 2 Sensor
Buzzer (ຂາ +)     ຂາ 9 ໃຊ້ tone() ສົ່ງສຽງ
LED Player 1 (ຂາ +)     ຂາ 10      LED ສີຂຽວ
LED Player 2 (ຂາ +)     ຂາ 6 LED ສີແດງ
Push Button ຂາ A0     ໃຊ້ INPUT_PULLUP ຫລືຜ່ານຕົວຕ້ານທານ 10kΩ ໄປ GND
![[Pasted image 20250715180123.png]]
![[Pasted image 20250715180130.png]]
![[Pasted image 20250715180135.png]]
#### code
```c++
#include <LiquidCrystal.h>

// LCD
LiquidCrystal lcd(12, 11, 5, 4, 3, 2);

// Ultrasonic
const int trig1 = 8, echo1 = 7;
const int trig2 = A2, echo2 = A3;

// LED, buzzer, ปุ่ม
const int led1 = 6, led2 = 10;
const int buzzer = 9, startBtn = A0;

void setup() {
  pinMode(trig1, OUTPUT); pinMode(echo1, INPUT);
  pinMode(trig2, OUTPUT); pinMode(echo2, INPUT);
  pinMode(led1, OUTPUT); pinMode(led2, OUTPUT);
  pinMode(buzzer, OUTPUT);
  pinMode(startBtn, INPUT_PULLUP);

  lcd.begin(16, 2);
  Serial.begin(9600);

  lcd.print("Reflex Fight!");
  Serial.println("Reflex Fight Ready!");
  delay(2000); lcd.clear();
}

void loop() {
  lcd.setCursor(0, 0); lcd.print("Press Start Btn");
  Serial.println("Waiting for Start...");
  while (digitalRead(startBtn)); // รอ

  readySignal();
  delay(random(2000, 5000));

  lcd.clear(); lcd.print("GO !!!");
  Serial.println("GO !!!");
  beep(2000, 500);

  waitForWinner();

  delay(3000);
  lcd.clear();
  digitalWrite(led1, LOW);
  digitalWrite(led2, LOW);
}

// ===== ฟังก์ชันย่อย =====

void readySignal() {
  lcd.clear(); lcd.print("Get Ready...");
  Serial.println("Get Ready...");
  beep(1000, 200); delay(200);
  beep(1000, 200);
}

void waitForWinner() {
  while (true) {
    int d1 = readDistance(trig1, echo1);
    int d2 = readDistance(trig2, echo2);

    if (d1 > 0 && d1 < 10) {
      announceWinner("Player 1 Win!", led1);
      break;
    } else if (d2 > 0 && d2 < 10) {
      announceWinner("Player 2 Win!", led2);
      break;
    }

    delay(10);
  }
}

void announceWinner(const char* msg, int ledPin) {
  lcd.clear(); lcd.print(msg);
  Serial.println(msg);
  digitalWrite(ledPin, HIGH);
  beep(2500, 500);
}

void beep(int freq, int dur) {
  tone(buzzer, freq, dur);
  delay(dur);
  noTone(buzzer);
}

int readDistance(int trig, int echo) {
  digitalWrite(trig, LOW); delayMicroseconds(2);
  digitalWrite(trig, HIGH); delayMicroseconds(10);
  digitalWrite(trig, LOW);
  long duration = pulseIn(echo, HIGH, 30000);
  if (duration == 0) return -1;
  return duration * 0.034 / 2;
}

```


#### ນີ້ຄື Tinkercad ຂອງ **Reflex Fight**
[Tinkercad ຂອງນ້ອງເອງ **Reflex Fight**](https://www.tinkercad.com/things/5ku9ympAcEt-phetnakhonekhamphan-reflex-fight?sharecode=OkOKiuLKwBnHOvhglImLGrLOJglBvrZKRY0RmR-cnxw)
![[Pasted image 20250715180143.png]]

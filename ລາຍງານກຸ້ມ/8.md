# **ລາຍງານໂຄງການ: ລະບົບກວດຈັບການເກີດອຸບັດຕິເຫດໃນຫ້ອງນໍ້າທ.ອານຸສັກ ບຸດສະດີ ລຳດັບ:12

ເພື່ອພັດທະນາລະບົບກວດຈັບອຸບັດຕິເຫດພາຍໃນຫ້ອງນໍ້າ ທີ່ສາມາດກວດຈັບການເຄື່ອນໄຫວ ແລະ ການມີຕົວຕົນຂອງຜູ້ໃຊ້ໄດ້ຢ່າງແມ່ນຍຳ ໂດຍອັດຕະໂນມັດ ເພື່ອແຈ້ງເຕືອນໃນກໍລະນີທີ່ເກີດສະຖານະການຕ່າງໆ ຫຼື ຄົນລົ້ມໃນຫ້ອງນໍ້າ ເພື່ອຫຼຸດຜ່ອນອັນຕະລາຍ ແລະ ຊ່ວຍໃຫ້ການຊ່ວຍເຫຼືອເກີດຂຶ້ນໄດ້ທັນເວລາ.
#### ອຸປະກອນ
•Arduino UNO – ໃນການຄວບຄຸມທຸກພາກສ່ວນ
•HC-SR04 – ultrasonic sensor ສຳລັບວັດລະຍະຫ່າງ
•PIR Motion Sensor – ກວດຈັບການເຄື່ອນໄຫວ
•Buzzer – ສຳລັບສົ່ງສັນຍານແຈ້ງເຕືອນ
•LED (Red/Green) – ສະແດງສະຖານະ
•Potentiometer – ກຳນົດເວລານັບຖອຍຫຼັງ
•Push Button – ສຳລັບ reset ລະບົບ
•7-Segment Display – ສະແດງຄ່າເວລານັບຖອຍຫຼັງ
•Breadboard ແລະ ສາຍ jumper – ສຳລັບເຊື່ອມຕໍ່ທຸກພາກສ່ວນ
#### ການເຊື່ອມວົງຈອນ
**ອຸປະກອນ**                    **Arduino Pin**
PIR Sensor (OUT)           D2
Buzzer (+)                  D3
LED Green                  D4
LED Red                   D5
7-Segment (A–G)            D6–D12
HC-SR04 Trig/Echo          A1 / A2
Button (Pull-up)              A3
Potentiometer (Center)        A0
![[Pasted image 20250715175315.png]]
![[Pasted image 20250715175326.png]]
![[Pasted image 20250715175123.png]]
#### code
```c++
#define PIR_PIN 2
#define BUZZER_PIN 3
#define LED_GREEN_PIN 4
#define LED_RED_PIN 5

const int segmentPins[] = {6, 7, 8, 9, 10, 11, 12}; 

#define TRIG_PIN A1
#define ECHO_PIN A2
#define BUTTON_PIN A3
#define POTENTIOMETER_PIN A0

int segmentDigits[10][7] = { 
  {1,1,1,1,1,1,0}, 
  {0,1,1,0,0,0,0}, 
  {1,1,0,1,1,0,1}, 
  {1,1,1,1,0,0,1}, 
  {0,1,1,0,0,1,1}, 
  {1,0,1,1,0,1,1}, 
  {1,0,1,1,1,1,1}, 
  {1,1,1,0,0,0,0}, 
  {1,1,1,1,1,1,1}, 
  {1,1,1,1,0,1,1}  
};

bool motionDetected = false;
bool countdownStarted = false;
bool alarmActive = false;
unsigned long countdownStartTime = 0;
int countdownTime = 0; 

void setup() {
  Serial.begin(9600);

  pinMode(PIR_PIN, INPUT);
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);
  pinMode(BUZZER_PIN, OUTPUT);
  pinMode(LED_GREEN_PIN, OUTPUT);
  pinMode(LED_RED_PIN, OUTPUT);
  pinMode(BUTTON_PIN, INPUT_PULLUP);

  for (int i = 0; i < 7; i++) {
    pinMode(segmentPins[i], OUTPUT);
  }

  digitalWrite(LED_GREEN_PIN, LOW);
  digitalWrite(LED_RED_PIN, LOW);
  digitalWrite(BUZZER_PIN, LOW);
}

void loop() {
  bool buttonPressed = digitalRead(BUTTON_PIN) == LOW;
  motionDetected = digitalRead(PIR_PIN);
  float distance = readUltrasonic();

  if (buttonPressed) {
    resetSystem();
    return;
  }

  if (!countdownStarted && motionDetected) {
    digitalWrite(LED_GREEN_PIN, HIGH);

    int potValue = analogRead(POTENTIOMETER_PIN);
    countdownTime = map(potValue, 0, 1023, 0, 9);
    displayNumber(countdownTime);
    delay(300); 
  }

  else if (!motionDetected && distance < 150 && !countdownStarted) {
    countdownStarted = true;
    countdownStartTime = millis();
  }

  if (countdownStarted && !alarmActive) {
    unsigned long elapsed = (millis() - countdownStartTime) / 1000;
    int remaining = countdownTime - elapsed;

    if (remaining >= 0) {
      displayNumber(remaining);
    } else {
      triggerAlarm();
    }
  }

  if (alarmActive) {
    digitalWrite(LED_RED_PIN, millis() / 300 % 2);
  }
}

float readUltrasonic() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);
  long duration = pulseIn(ECHO_PIN, HIGH);
  float distance = duration * 0.034 / 2;
  return distance;
}

void displayNumber(int number) {
  if (number < 0) number = 0;
  if (number > 9) number = 9;

  for (int i = 0; i < 7; i++) {
    digitalWrite(segmentPins[i], segmentDigits[number][i]);
  }
}

void triggerAlarm() {
  alarmActive = true;
  digitalWrite(BUZZER_PIN, HIGH);
  digitalWrite(LED_GREEN_PIN, LOW);
  digitalWrite(LED_RED_PIN, HIGH); 
}

void resetSystem() {
  countdownStarted = false;
  alarmActive = false;
  countdownStartTime = 0;
  digitalWrite(LED_GREEN_PIN, LOW);
  digitalWrite(LED_RED_PIN, LOW);
  digitalWrite(BUZZER_PIN, LOW);
  displayNumber(0);
}

```
#### **ວິທີການເຮັດວຽກ**
ເລີ່ມຕົ້ນ:
•ລະບົບຈັບການເຄື່ອນໄຫວ (PIR)
•ຖ້າຈັບໄດ້ → ສະແດງເວລາ countdown ຈາກ potentiometer → LED Green ON
ຖ້າບໍ່ມີການເຄື່ອນໄຫວ ແລະ ລະຍະຫ່າງ < 150cm:→ ເລີ່ມນັບຖອຍຫຼັງ
**ໝົດເວລາ ແຕ່ຍັງບໍ່ເຄື່ອນໄຫວ:**
→ Buzzer ON
→ LED Red Flash
→ ເປັນການແຈ້ງເຕືອນອຸບັດຕິເຫດ
**Reset System:**
→ ກົດປຸ່ມ → ປິດ Buzzer, LED, ແລະ reset ຕົວນັບ.
#### ນີ້ຄື Tinkercad ຂອງນ້ອງທີ່ທົດລອງ
[Tinkercad ຂອງນ້ອງເອງ Project-midtrem](https://www.tinkercad.com/things/js5UGhfAQqc-project-midtrem)
![[Pasted image 20250715175152.png]]
